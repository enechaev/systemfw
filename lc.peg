# typed lambda calculus

%prefix "lc"
%value "struct lambda *"
%auxil "struct debruijn *"
%header {
    #include "context.h" 
    #include "lambda.h" 
    #include "type.h"
    #include "debruijn.h" 
}
%source {
    #ifndef PCC_ERROR
    #define PCC_ERROR(auxil) pcc_error(auxil)
    static void pcc_error(struct debruijn *auxil) {
        fprintf(stderr, "Syntax error\n");
        if(auxil) debruijn_destroy(*auxil);
        exit(1);
    }
    #endif /* PCC_ERROR */
}

main        <- ID EQUAL term
             / _ '?' EQUAL term
term        <- (t:application / t:abstraction) {$$=t;}
abstraction <- LAMBDA ID:ID (OFTYPE TID:tterm)? {if (indexof(auxil,ID->atom.s)) PCC_ERROR(auxil); debruijn_push(auxil,ID->atom.s);}
                            DOT T:term {
                                        debruijn_pop(auxil);
                                        $$=(struct lambda *)malloc(sizeof(struct lambda));
                                        if (!$$) PCC_ERROR(auxil);
                                        $$->t=LAMBDA_ABSTR;
                                        $$->abstr.v=ID->atom.s; free(ID);
                                        $$->abstr.type=TID;
                                        $$->abstr.expr=T;
                                      }
application <- S:application T:atom {
                                        $$=(struct lambda *)malloc(sizeof(struct lambda));
                                        if (!$$) PCC_ERROR(auxil);
                                        $$->t=LAMBDA_APPL;
                                        $$->appl.lhs=S;
                                        $$->appl.rhs=T;
                                    }
             / T: atom {$$=T;}
atom        <- ID:ID {$$=ID;}
             / LP T:term RP {$$=T;}
ID          <- _ <[A-Za-z./_:*+=-][A-Za-z0-9./_:*+=-]*> _ {
                                                $$=(struct lambda *)malloc(sizeof(struct lambda));
                                                if (!$$) PCC_ERROR(auxil);
                                                $$->t=LAMBDA_ATOM;
                                                $$->atom.s=strdup($1);
                                                if (!$$->atom.s) PCC_ERROR(auxil);
                                                $$->atom.index=indexof(auxil,$1);
                                           }
tterm       <- tatom (ARROW tterm)?
tatom       <- ID:ID
             / LP T:tterm RP {$$=T;}
EQUAL       <- _ '=' _
ARROW       <- _ '->' _
LAMBDA      <- _ '\\' _
OFTYPE      <- _ ':' _
DOT         <- _ '.' _
LP          <- _ '(' _
RP          <- _ ')' _
_           <- [\n\t\r ]*

